class BinaryCipher {
    function String encrypt(String input){
        var int length;
        var String result;
        var int i;
        var int charValue;
        var String binaryChar;
        var int j;

        let length = input.length();
        let result = String.new(length * 9);

        let i = 0;
        while (i < length){
            let charValue = input.charAt(i);
            let binaryChar = BinaryCipher.toBinary(charValue);

            let j = 0;
            while (j < binaryChar.length()){
                do result.appendChar(binaryChar.charAt(j));
                let j = j + 1;
            }

            do result.appendChar(32); // Пробел между двоичными числами
            let i = i + 1;
        }

        return result;
    }


    function String toBinary(int value){
        var String binary;
        var String temp;
        var int remainder;
        var int i;

        let binary = String.new(8);

        if (value = 0){
            do binary.appendChar(48); // Если значение 0, добавляем "0"
        } else {
            while (value > 0){
                let remainder = value - ((value / 2) * 2);

                let temp = String.new(binary.length() + 1);
                do temp.appendChar(48 + remainder);

                let i = 0;
                while (i < binary.length()){
                    do temp.appendChar(binary.charAt(i));
                    let i = i + 1;
                }

                let binary = temp;
                let value = value / 2;
            }
        }

        while (binary.length() < 8){
            let temp = String.new(binary.length() + 1);
            do temp.appendChar(48);

            let i = 0;
            while (i < binary.length()){
                do temp.appendChar(binary.charAt(i));
                let i = i + 1;
            }

            let binary = temp;
        }

        return binary;
    }

    function String decrypt(String binaryInput) {
        var int length;
        var String result;
        var String binaryChar;
        var int charValue;
        var int i;
        var int j;

        let length = binaryInput.length();
        let result = String.new(length / 8); // Результат расшифровки

        let i = 0;
        while (i < length) {
            // Извлекаем 8 символов (один байт)
            let binaryChar = String.new(8);
            let j = 0;
            while (j < 8) {
                do binaryChar.appendChar(binaryInput.charAt(i + j));
                let j = j + 1;
            }

            // Преобразуем из двоичной строки в число
            let charValue = BinaryCipher.binaryToInt(binaryChar);

            // Добавляем символ к результату
            do result.appendChar(charValue);

            let i = i + 9; // Переход к следующей группе (учитываем пробел)
        }

        return result;
    }

    function int binaryToInt(String binary) {
        var int value;
        var int length;
        var int i;

        let length = binary.length();
        let value = 0;

        let i = 0;
        while (i < length) {
            let value = value * 2 + (binary.charAt(i) - 48); // Преобразование из '0'/'1' в число
            let i = i + 1;
        }

        return value;
    }
}
